breed [vehicles vehicle] ; Define a new breed called "vehicles"

vehicles-own [
  location            ; Current location of the vehicle
  destination         ; Destination of the vehicle
  top-speed           ; Maximum speed of the vehicle
  speed               ; Current speed of the vehicle
  speed-restriction   ; Local speed restriction
  journey            ; Total distance of the current journey
  remaing-journey     ; Remaining distance of the current journey
]

to vehicles-init [number-of-vehicles]
  create-vehicles number-of-vehicles [
    set color lime                     ; Set vehicle color
    set shape "default"               ; Set vehicle shape
    let new-location one-of nodes
    let new-road 0

    ; Assign a random destination and road to the vehicle
    ask new-location [
      set new-road one-of my-roads
    ]
    set-destination new-location new-road 

    set top-speed random-float 0.9 + 0.1  ; Set random maximum speed
    set speed top-speed                  ; Set initial speed to maximum speed
    set remaing-journey journey   ; Set initial remaining journey
    fd speed
    set speed-restriction [speed_limmit] of new-road
  ]
end

to vehicles-move
  let visible-neighbors (other turtles in-cone 0.2 10) ; Identify nearby vehicles
  let ahead nearest-vehicle-traverling-in-same-direction ; Find the nearest vehicle ahead

  ; Adjust speed based on traffic conditions
  set speed top-speed
  set color lime
  if ahead != nobody [
    ifelse distance ahead < speed [
      set speed 0 ; Stop if too close
      set color red ; Indicate stopped
    ] [
      if distance ahead < 2 * speed [
        let ahead-speed [speed] of ahead ; Get the speed of the vehicle ahead
        if ahead-speed <= speed [
          set speed ahead-speed  ; Reduce speed to avoid collision
        ]
        set color orange ; Indicate reduced speed
      ]
    ]
  ]

  ; If the vehicle reaches its destination, find a new one
  if distance location >= journey [
    let difference distance location - journey ; Calculate remaining distance
    let new-road 0
    ask destination [
      set new-road one-of my-roads
    ]
    ; Set a new destination and road
    set-destination destination new-road
    set speed-restriction [speed_limmit] of new-road
    set speed difference                       
  ]

  ; Enforce speed limit
  if speed > speed-restriction [
    set speed speed-restriction
  ]

  fd speed ; Move forward
  set remaing-journey distance destination ; Update remaining journey distance
end

to-report nearest-vehicle-traverling-in-same-direction
  report min-one-of other vehicles with [  
    destination = [destination] of myself and  ; Same destination
    remaing-journey < [remaing-journey] of myself and  ; Shorter remaining journey
    heading = [heading] of myself ; Same direction
  ] [distance myself]
end

to set-destination [new-location new-road]
  let new-destination 0
  ;select the correct end of the road for the new destination
  ask  new-road [  
      ifelse end1 = new-location[
         set new-destination end2
      ][
        set new-destination end1
      ]
  ]
  set location new-location
  set destination new-destination
  move-to location                   ; Move to the new location
  face destination                   ; Face the new destination
  set journey distance destination
end